apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

android {
    def globalConfiguration = rootProject.extensions.getByName("ext");

    compileSdkVersion globalConfiguration.getAt("androidCompileSdkVersion")
    buildToolsVersion "23.0.1"

    defaultConfig {
        minSdkVersion globalConfiguration.getAt("androidMinSdkVersion")
        targetSdkVersion globalConfiguration.getAt("androidTargetSdkVersion")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

}

dependencies {
    def presentationDependencies = rootProject.ext.appDependencies

    // supportV7的包中的特有的一些控件
    compile presentationDependencies.recyclerView
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

// Build a jar file in addition to the default aar file
android.libraryVariants.all {
    variant ->
        def name = variant.buildType.name
        def task = project.tasks.create "jar${name.capitalize()}", Jar
        task.dependsOn variant.javaCompile
        task.from variant.javaCompile.destinationDir
        artifacts.add('archives', task);
}

apply from: '../buildsystem/gradle_mvn_push.gradle'